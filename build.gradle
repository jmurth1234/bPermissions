buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.4'
    }
}

ext {
    buildNumber = System.getenv('GITHUB_RUN_NUMBER') ?: 'SNAPSHOT'
    baseVersion = '2.13.0'
    // For release builds, use just the base version without suffix
    // For development builds, append build number and -SNAPSHOT suffix
    if (buildNumber == 'release') {
        fullVersion = baseVersion
    } else if (buildNumber == 'SNAPSHOT') {
        fullVersion = "${baseVersion}-SNAPSHOT"
    } else {
        fullVersion = "${baseVersion}-${buildNumber}-SNAPSHOT"
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'maven-publish'

    group = 'de.bananaco'
    version = rootProject.ext.fullVersion

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/rymate1234/bPermissions")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
