buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.4'
    }
}

ext {
    buildNumber = System.getenv('GITHUB_RUN_NUMBER') ?: 'dev'
    baseVersion = '2.13.0'
    // For release builds, use just the base version without suffix
    // For development builds, just append build number (GitHub Packages doesn't support SNAPSHOT)
    if (buildNumber == 'release') {
        fullVersion = baseVersion
    } else {
        fullVersion = "${baseVersion}-${buildNumber}"
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'maven-publish'

    group = 'de.bananaco'
    version = rootProject.ext.fullVersion

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/jmurth1234/bpermissions")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
